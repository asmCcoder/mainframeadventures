//REWRTSEQ JOB (COBOL),
//             'WRITE TO SEQUENTIAL',
//             CLASS=A,
//             MSGCLASS=H,
//             REGION=8M,TIME=1440,
//             MSGLEVEL=(1,1)
//COMPCOB  EXEC COBUCG,
//         PARM.COB='FLAGW,LOAD,SUPMAP,SIZE=2048K,BUF=1024K'
//COB.SYSPUNCH DD DUMMY
//COB.SYSIN    DD *
       IDENTIFICATION DIVISION.
       PROGRAM-ID. REWRTSEQ.
      ******************************************************************
       ENVIRONMENT DIVISION.
      ******************************************************************
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT IN-FILE ASSIGN TO DA-S-INFILE.
      ******************************************************************
       DATA DIVISION.
      ******************************************************************
       FILE SECTION.
       FD  IN-FILE
           RECORDING MODE F
           LABEL RECORDS ARE STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORD CONTAINS 52 CHARACTERS.
       01  IN-RECORD.
           05 EMP-UID         PIC 9999.
           05 EMP-NAME        PIC A(20).
           05 EMP-CITY        PIC A(10).
           05 EMP-COUNTRY     PIC A(10).
           05 EMP-DOB.
              10 EMP-DOB-YYYY PIC 9999.
              10 EMP-DOB-MM   PIC 99.
              10 EMP-DOB-DD   PIC 99.
      **********************************
       WORKING-STORAGE SECTION.
      **********************************
       77  END-OF-FILE        PIC X VALUE 'N'.
           88 EOF                   VALUE 'Y'.
       01  COUNTERS.
           05 REC-COUNTER     PIC 9(3) VALUE 0.
      ******************************************************************
       PROCEDURE DIVISION.
      ******************************************************************
       000-MAIN.
           OPEN I-O IN-FILE.
           PERFORM 100-READ-FILE THRU 199-READ
               UNTIL EOF.
           DISPLAY 'TOTAL RECORDS: ' REC-COUNTER.
           CLOSE IN-FILE.
           STOP RUN.
      **********************************
       100-READ-FILE.
      **********************************
           READ IN-FILE
               AT END MOVE 'Y' TO END-OF-FILE.
           IF NOT EOF THEN
             PERFORM 300-DISPLAY-RECORD
             ADD 1 TO REC-COUNTER.
           IF EMP-UID = 0003
             PERFORM 200-CHANGE-RECORD.
       199-READ.
           EXIT.
      **********************************
       200-CHANGE-RECORD.
      **********************************
           MOVE 'JANE DOE' TO EMP-NAME.
           MOVE 'DUBLIN'   TO EMP-CITY.
           MOVE 'IRELAND'  TO EMP-COUNTRY.
           MOVE 1969       TO EMP-DOB-YYYY.
           MOVE 3          TO EMP-DOB-MM.
           MOVE 27         TO EMP-DOB-DD.
           DISPLAY '**** RECORD CHANGED ****'
           PERFORM 300-DISPLAY-RECORD.
           DISPLAY '************************'
           REWRITE IN-RECORD.
      **********************************
       300-DISPLAY-RECORD.
      **********************************
             DISPLAY 'NAME    : ' EMP-NAME.
             DISPLAY 'CITY    : ' EMP-CITY.
             DISPLAY 'COUNTRY : ' EMP-COUNTRY.
             DISPLAY 'BIRTHDAY  '.
             DISPLAY '     DAY: ' EMP-DOB-DD.
             DISPLAY '   MONTH: ' EMP-DOB-MM.
             DISPLAY '    YEAR: ' EMP-DOB-YYYY.
             DISPLAY '************************'.
/*
//GO.INFILE   DD DISP=SHR,DSN=HERC01.SEQ.EMPLOYEE
//GO.SYSOUT   DD SYSOUT=*
//
